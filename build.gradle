import com.github.spotbugs.SpotBugsTask

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath("com.bmuschko:gradle-nexus-plugin:2.3.1")
    }
}

plugins {
    id 'java-library'
    id 'com.bmuschko.nexus' version '2.3.1'
    id "com.github.spotbugs" version "1.6.5"
    id 'com.github.sherter.google-java-format' version '0.7.1'
}

group = "com.github.nomadblacky"
version = "0.4.0"
description = "Configure sentry-java by typesafe-config."

sourceCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
    api 'io.sentry:sentry:1.7.16'
    api 'com.typesafe:config:1.3.3'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.2'
    testCompile 'org.assertj:assertj-core:3.11.1'
    testCompile 'org.mockito:mockito-core:2.24.0'
    testCompile 'org.mockito:mockito-junit-jupiter:2.23.4'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.3.2'
    testRuntime 'org.slf4j:slf4j-simple:1.7.25'
}

modifyPom {
    project {
        name 'sentry-config'
        description 'Configure sentry-java by typesafe-config.'
        url 'https://github.com/NomadBlacky/sentry-config'
        inceptionYear '2018'

        scm {
            url 'https://github.com/NomadBlacky/sentry-config'
            connection 'scm:https://NomadBlacky@github.com/NomadBlacky/sentry-config.git'
            developerConnection 'scm:git://github.com/NomadBlacky/sentry-config.git'
        }

        licenses {
            license {
                name 'MIT License'
                url 'https://opensource.org/licenses/mit-license.php'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'NomadBlacky'
                name 'Takumi Kadowaki'
                email 'hourou.hamurinzu@gmail.com'
            }
        }
    }
}

spotbugs {
    toolVersion = '3.1.8'
}

tasks.withType(SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
    pluginClasspath = project.configurations.spotbugsPlugins
}
